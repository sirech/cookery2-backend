buildscript {
    ext {
        kotlinVersion = '1.3.50'
        springBootVersion = '2.1.7.RELEASE'
        jacksonVersion = '2.9.9'
        swaggerVersion = '2.9.2'
        exposedVersion = '0.17.2'
        flywayVersion = '6.0.0'
        junitVersion = '5.5.1'
        mockkVersion = '1.9.3'
        springMockkVersion = '1.1.2'
        striktVersion = '0.21.1'
        detektVersion = '1.0.1'
    }
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath "com.github.ben-manes:gradle-versions-plugin:0.21.0"

        // Verify pacts from gradle
        classpath 'au.com.dius:pact-jvm-provider-gradle_2.12:3.6.12'

        // Linter
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${detektVersion}"
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'au.com.dius.pact'
apply plugin: 'io.gitlab.arturbosch.detekt'

group = 'com.hceris'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.data:spring-data-commons'

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:${jacksonVersion}"

    implementation "org.jetbrains.exposed:exposed:$exposedVersion"
    implementation "org.jetbrains.exposed:spring-transaction:$exposedVersion"
    implementation "org.flywaydb:flyway-core:$flywayVersion"


    implementation "io.springfox:springfox-swagger2:$swaggerVersion"
    implementation "io.springfox:springfox-swagger-ui:$swaggerVersion"

    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testCompile "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"

    testImplementation "io.strikt:strikt-core:$striktVersion"
    testImplementation "io.mockk:mockk:${mockkVersion}"
    testImplementation "com.ninja-squad:springmockk:${springMockkVersion}"
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '1.8'
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '1.8'
    }
}

test {
    useJUnitPlatform()
    dependsOn 'cleanTest'
    testLogging {
        events "passed", "skipped", "failed"
    }
}

pact {
    serviceProviders {
        api {
            port = 4003
            def pactFile = file('../ui/pacts/sar-ui-entrypoint-api.json')
            hasPactWith('cookery2-frontend') {
                pactSource = file('../cookery2-frontend/pacts/frontend-backend.json')
            }
        }
    }
}

detekt {
    toolVersion = "${detektVersion}"
    input = files("src/main/kotlin", "src/test/kotlin")
}
