buildscript {
    ext {
        kotlinVersion = '2.1.21'
        springBootVersion = '4.0.1'
        jacksonVersion = '2.12.3'
        springdocVersion = '2.6.0'
        exposedVersion = '0.52.0'
        arrowVersion = '0.12.1'
        jwkSetVersion = '5.1'
        auth0Version = '3.14.0'
        flywayVersion = '10.17.2'
        junitVersion = '5.11.3'
        mockkVersion = '1.13.3'
        springMockkVersion = '3.0.1'
        striktVersion = '0.31.0'
        detektVersion = '1.23.6'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath "com.github.ben-manes:gradle-versions-plugin:0.53.0"

        // Verify pacts from gradle
        classpath 'au.com.dius.pact.provider:gradle:4.7.0-beta.3'

        // Linter
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${detektVersion}"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'au.com.dius.pact'
apply plugin: 'io.gitlab.arturbosch.detekt'

group = 'com.hceris'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.data:spring-data-commons'
    implementation 'com.mysql:mysql-connector-j'

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:${jacksonVersion}"

    implementation "org.jetbrains.exposed:exposed-core:$exposedVersion"
    implementation "org.jetbrains.exposed:exposed-dao:$exposedVersion"
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposedVersion"
    implementation "org.jetbrains.exposed:exposed-java-time:$exposedVersion"
    implementation "org.jetbrains.exposed:exposed-spring-boot-starter:$exposedVersion"
    implementation "org.flywaydb:flyway-core:$flywayVersion"
    runtimeOnly "org.flywaydb:flyway-mysql:$flywayVersion"

    implementation("com.nimbusds:nimbus-jwkset-loader:$jwkSetVersion")
    implementation("com.auth0:java-jwt:$auth0Version")

    implementation("io.arrow-kt:arrow-core:$arrowVersion")
    implementation("io.arrow-kt:arrow-fx:$arrowVersion")

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$springdocVersion"

    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.junit.platform:junit-platform-commons:1.11.3"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.11.3"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"

    testImplementation "io.strikt:strikt-core:$striktVersion"
    testImplementation "io.mockk:mockk:${mockkVersion}"
    testImplementation "com.ninja-squad:springmockk:${springMockkVersion}"
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '17'
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '17'
    }
}

jar {
    enabled = false
}

test {
    useJUnitPlatform()
    dependsOn 'cleanTest'
    testLogging {
        events "passed", "skipped", "failed"
    }
}

pact {
    serviceProviders {
        api {
            port = 4003

            hasPactWith('cookery2-frontend') {
                pactSource = file("$buildDir/pacts/frontend-backend.json")
                // pactSource = file('../cookery2-frontend/pacts/frontend-backend.json')
                stateChangeUrl = url("http://localhost:$port/pact")
            }
        }
    }
}

def pactFile = file("$buildDir/pacts/frontend-backend.json")

tasks.register('downloadPact') {
    outputs.file(pactFile)
    doLast {
        pactFile.parentFile.mkdirs()
        new URL('https://raw.githubusercontent.com/sirech/cookery2-frontend/master/pacts/frontend-backend.json')
            .withInputStream { input ->
                pactFile.withOutputStream { output ->
                    output << input
                }
            }
    }
}

tasks.withType(au.com.dius.pact.provider.gradle.PactVerificationTask).configureEach {
    dependsOn tasks.named('downloadPact')
    doNotTrackState("Pact verification tasks are not cacheable and do not declare all inputs.")
}

gradle.projectsEvaluated {
    tasks.withType(au.com.dius.pact.provider.gradle.PactVerificationTask).configureEach {
        testClasspathURL.set(project.sourceSets.test.runtimeClasspath.files.collect { it.toURI() })
        testClasspathURL.disallowChanges()
    }
}


detekt {
    toolVersion = "${detektVersion}"
    input = files("src/main/kotlin", "src/test/kotlin")
    config = files("detekt-config.yml")
}
